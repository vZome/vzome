

group = 'com.vzome'
version = '7.1'

task wrapper(type: Wrapper) {
   gradleVersion = '4.5.1' //version required
}

// These environment variables are automatically available during a Jenkins build
ext.buildNumber = System.getenv( "BUILD_NUMBER" ) ?: 'dev'
ext.gitCommit = System.getenv( "GIT_COMMIT" ) ?: 'HEAD'
ext.githubRepoName = System.getenv( "GITHUB_REPO_NAME" ) ?: ''

// These environment variables must be configured in the Jenkins job explicitly.
ext.githubClientId = System.getenv( "GITHUB_CLIENT_ID" )
ext.githubClientSecret = System.getenv( "GITHUB_CLIENT_SECRET" )

ext.jreHome = System.getenv( "JAVA_HOME" ) ?: '/Library/Java/JavaVirtualMachines/adoptopenjdk-8.jdk/Contents/Home'

ext.appName = 'vZome'
ext.edition = appName

// Specify common settings for all subprojects
subprojects {

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse'

  compileJava {
	// set these to 9 for Java 9
	// set them to 1.8 for Java 8
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenLocal()
    mavenCentral()
    
    // This is for the JSweet compiler
    maven { url "http://repository.jsweet.org/artifactory/libs-release-local" }
    maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }

    // this one is for javax.media.jai
    maven { url 'http://repository.springsource.com/maven/bundles/external' }

    // For EGit
    maven { url 'https://repo.eclipse.org/content/groups/releases' }

    ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR downloads for JOGL
      url "http://jogamp.org/deployment"
      layout "pattern", {
        artifact "[revision]/jar/[artifact].[ext]"
      }
    }
    ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR download for vecmath
      url "http://jogamp.org/deployment"
      layout "pattern", {
        artifact "[organization]/[revision]/[artifact].[ext]"
      }
    }
  }

  // see platform/mac/build.gradle, search for "error -5341"
  //    buildDir    = rootProject.buildDir
  group       = rootProject.group
  version     = rootProject.version
  description = rootProject.name + '-' + project.name
}

project(':desktop') {
    dependencies {
        compile project(':core')
    }
}

project(':server') {
    dependencies {
        compile project(':desktop')
    }
}
