plugins {
    id 'java'
    id 'edu.sc.seis.macAppBundle' version '2.1.0'
}

compileJava {
    // The ignore.symbol.file is evidently necessary to allow the com.apple.eawt classes inside lib/rt.jar
    // to be visible.
    // See http://mail.openjdk.java.net/pipermail/macosx-port-dev/2011-September/000864.html
    // and http://stackoverflow.com/questions/12554829/passing-arguments-to-compiler-and-javadoc-in-gradle
    // "I only managed to get this working when I forked 
    // and passed 'javac' as an executable"
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    compile project( ':' )
}

// The classesDir adjustment is necessary so that mac-5.0.jar does not end up containing
//  all the classes of vzome-desktop.  The correct fix is actually in vzome-desktop/build.gradle,
//  to remove the common buildDir for all subprojects.  However, applying that fix causes a
//  mysterious failure from "createDmg":
//       hdiutil: create failed - error -5341
//  I am unable to determine what the error code means.  After much experimentation, the
//  failure correlates with a size-optimized mac-5.0.jar.  If I leave in all or even some of
//  the extraneous classes and resources, the error does not appear.  Therefore I have
//  found an acceptable compromise.  For classes, I use a separate directory, configured
//  here.  For resources, I continue to use the master build/main/resources directory.
//  Finally, to minimize unnecessary bloat, I am removing all the old, unused splash images
//  from the source.
sourceSets.main.output.classesDir 'build/classes'

// for the 'macappbundle' Gradle plugin
macAppBundle {
    appName = project.appName
    dmgName = "${appName}-MacOSX-${->project.version}"
    volumeName = "${dmgName}-build-${->project.buildNumber}"
    delegate.mainClassName = 'com.vzome.platform.mac.Adapter'
    creatorCode = 'vZ50'
    icon = 'vZomeLogo.icns'
    bundleJRE = 'true'
    javaProperties.putAll jvmArgs
    javaProperties.put 'java.ext.dirs', '.'
    arguments.addAll vzomeArgs
    bundleExtras = [ CFBundleDocumentTypes: [ [
        CFBundleTypeExtensions: [ project.appName ],
        CFBundleTypeIconFile: 'vZomeLogo.icns',
        CFBundleTypeName: project.appName + ' model',
        CFBundleTypeRole: 'Editor',
        LSTypeIsPackage: 'false'
    ] ] ]
    // now make sure we add '-Xmx3048M', which does not work as in either javaProperties or arguments
    generatePlist << {
        def parser = new XmlParser( false, false, true )
        parser .setFeature( "http://apache.org/xml/features/nonvalidating/load-external-dtd", false )
        def plistFile = getPlistFile()
        def plist = parser.parse( plistFile )

        plist.dict.array[0].appendNode(
            new groovy.xml.QName("string"),
            [:],
            '-Xmx3048M'
        )
        def writer = new FileWriter( plistFile )
        groovy.xml.XmlUtil .serialize( plist, writer )
    }
}
// the macAppBundle extension adds this unwanted dependency
assemble .dependsOn .remove( createDmg )
//println assemble.taskDependencies.values

